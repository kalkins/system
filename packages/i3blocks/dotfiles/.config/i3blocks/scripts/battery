#!/bin/sh

DEVICE="${BLOCK_INSTANCE:-battery_BAT0}"

DEV_PATH=$(upower -e | grep "${DEVICE}")

upower -i "${DEV_PATH}" | awk '
BEGIN {
    percentage = 0
    hours = -1
    minutes = -1
    status = "ERR"
}
/state/ {
    if ($2 == "discharging") {
	status = "DIS"
    } else if ($2 == "charging") {
	status = "CHR"
    } else if ($2 == "fully-charged") {
        status = "CHR"
    }
}
/time to/ {
    if ($5 == "minutes") {
	minutes = $4
	hours = 0
    } else {
	time = 60 * $4
	minutes = time % 60
	hours = (time-minutes) / 60
    }
}
/percentage/ {
    percentage=int($2)
}
END {
    if (status == "ERR") {
	exit 1
    }

    # Long text
    if (hours != -1 && minutes != -1) {
	printf(" %d%% %s (%02d:%02d)\n", percentage, status, hours, minutes)
    } else {
	printf(" %d%% %s\n", percentage, status, hours, minutes)
    }

    # Short text
    printf(" %d%%\n", percentage)

    if (status == "CHR") {
	printf("#00FF00\n")
    } else {
	if (percentage > 90)
	    printf("#00FF00\n")
	else if (percentage > 70)
	    printf("#55FF00\n")
	else if (percentage > 50)
	    printf("#A8FF00\n")
	else if (percentage > 30)
	    printf("#FFF600\n")
	else if (percentage > 10)
	    printf("#FFAE00\n")
	else if (percentage > 5)
	    printf("#FF0000\n")
	else {
	    printf("#FF0000\n")
	    exit 33
	}
    }
}
'
